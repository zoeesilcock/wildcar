const std = @import("std");
const flint = @import("flint");

const TARGETS = [_]std.Target.Query{
    .{ .cpu_arch = .x86_64, .os_tag = .windows, .abi = .gnu },
    .{ .cpu_arch = .x86_64, .os_tag = .linux, .abi = .gnu },
    .{ .cpu_arch = .aarch64, .os_tag = .macos },
};
const OPTIMIZE_MODES = [_]std.builtin.OptimizeMode{ .Debug, .ReleaseFast };
const INTERNAL_MODES = [_]bool{ true, false };

const SHADER_FORMATS: []const []const u8 = &.{ "spv", "msl", "dxil" };
const SHADERS: []const []const u8 = &.{
    "lambert.vert",
    "lambert.frag",
    "sky.frag",
    "sky.vert",
    "screen.vert",
    "screen.frag",
    "shadow.vert",
    "shadow.frag",
};

var log_allocations: bool = false;

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});
    const name = b.option([]const u8, "name", "name of the shared library") orelse "wildcar";
    const internal = b.option(bool, "internal", "include debug interface") orelse true;
    const lib_only = b.option(bool, "lib_only", "only build the shared library") orelse false;
    log_allocations = b.option(bool, "log_allocations", "log all allocations") orelse false;

    // Integrate Flint.
    const flint_options: flint.IntegrateOptions = .{
        .dependency = b.dependency("flint", .{ .target = target, .optimize = optimize, .internal = internal }),
        .target = target,
        .optimize = optimize,
        .build_options = b.addOptions(),
        .name = name,
        .internal = internal,
        .lib_only = lib_only,
        .install_step = b.getInstallStep(),
    };
    const result = flint.integrate(b, flint_options);

    // Install executable.
    if (result.exe) |exe| {
        b.getInstallStep().dependOn(&b.addInstallArtifact(exe, .{}).step);
    }

    // Game library.
    const module = b.createModule(.{
        .root_source_file = b.path("src/root.zig"),
        .target = flint_options.target,
        .optimize = flint_options.optimize,
    });
    const math_mod = b.createModule(.{
        .root_source_file = b.path("src/math.zig"),
        .target = flint_options.target,
        .optimize = flint_options.optimize,
        .imports = &.{
            .{ .name = "flint", .module = result.flint_mod },
        },
    });
    module.addImport("build_options", result.build_options_mod);
    module.addImport("flint", result.flint_mod);
    module.addImport("math", math_mod);
    addBox3D(b, target, optimize, module);

    const lib = b.addLibrary(.{
        .name = flint_options.name,
        .linkage = .dynamic,
        .root_module = module,
        .use_llvm = true,
    });

    // Install library.
    b.getInstallStep().dependOn(&b.addInstallArtifact(lib, .{}).step);

    // Check library.
    const lib_check = b.addLibrary(.{
        .linkage = .dynamic,
        .name = name,
        .root_module = lib.root_module,
    });
    const check = b.step("check", "Check if it compiles");
    check.dependOn(&lib_check.step);

    // Tests.
    const test_step = b.step("test", "Run unit tests");
    const lib_tests = b.addTest(.{ .root_module = lib.root_module });
    const run_lib_tests = b.addRunArtifact(lib_tests);
    test_step.dependOn(&run_lib_tests.step);

    // Build all variations.
    const build_all_step = b.step("all", "Builds all permutations of the game for testing purposes.");
    flint.buildMatrixDefault(b, build_all_step, name, b.path("assets"));

    // Shader compilation.
    const compile_shaders_step = b.step(
        "compile_shaders",
        "Compile SHADERS. (requires a working shadercross installation on the path)",
    );
    inline for (SHADERS) |shader| {
        inline for (SHADER_FORMATS) |shader_output_format| {
            const output_name = shader ++ "." ++ shader_output_format;
            var compile_shader = b.addSystemCommand(&.{"shadercross"});
            compile_shader.addFileArg(b.path("src/shaders/" ++ shader ++ ".hlsl"));
            compile_shader.addArg("-o");
            const compiled_shader = compile_shader.addOutputFileArg(output_name);
            const installed_shader = b.addInstallFile(compiled_shader, "../assets/shaders/" ++ output_name);

            compile_shaders_step.dependOn(&compile_shader.step);
            compile_shaders_step.dependOn(&installed_shader.step);
        }
    }

    b.getInstallStep().dependOn(compile_shaders_step);
}

fn addBox3D(
    b: *std.Build,
    target: std.Build.ResolvedTarget,
    optimize: std.builtin.OptimizeMode,
    module: *std.Build.Module,
) void {
    const box3d = b.dependency("box3d", .{});

    module.addIncludePath(box3d.path("include"));
    module.addCMacro("BOX3D_EXPORT", "");
    module.addCSourceFiles(.{
        .root = box3d.path("src"),
        .files = &.{
            "aabb.c",
            "arena_allocator.c",
            "bitset.c",
            "block_allocator.c",
            "body.c",
            "broad_phase.c",
            "capsule.c",
            "compound.c",
            "constraint_graph.c",
            "contact.c",
            "contact_solver.c",
            "convex_manifold.c",
            "core.c",
            "distance.c",
            "distance_joint.c",
            "dynamic_tree.c",
            "height_field.c",
            "hull.c",
            "id_pool.c",
            "island.c",
            "joint.c",
            "manifold.c",
            "math_functions.c",
            "mesh.c",
            "mesh_contact.c",
            "motor_joint.c",
            "mover.c",
            "name_cache.c",
            "parallel_for.c",
            "parallel_joint.c",
            "physics_world.c",
            "prismatic_joint.c",
            "recording.c",
            "recording_replay.c",
            "world_snapshot.c",
            "revolute_joint.c",
            "scheduler.c",
            "sensor.c",
            "shape.c",
            "simd.c",
            "solver.c",
            "solver_set.c",
            "sphere.c",
            "spherical_joint.c",
            "table.c",
            "timer.c",
            "triangle_manifold.c",
            "types.c",
            "weld_joint.c",
            "wheel_joint.c",
        },
        .flags = &.{
            "-std=gnu17",
            "-Wmissing-prototypes",
            "-Wall",
            "-Wextra",
            "-pedantic",
            "-Wno-unused-value",
            "-fno-sanitize=alignment",
            "-ffp-contract=off",
        },
    });

    const translate_c = b.addTranslateC(.{
        .root_source_file = b.path("src/c.h"),
        .target = target,
        .optimize = optimize,
    });
    translate_c.addIncludePath(box3d.path("include"));
    translate_c.defineCMacro("BOX3D_EXPORT", "");

    module.addImport("c", translate_c.createModule());
}
